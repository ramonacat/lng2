use crate::identifier::{Identifiers, Identifier};
use crate::types::function::UncheckedFunctionType;
use crate::types::function::UncheckedFunctionTypeKind;
use crate::ast;

grammar(identifiers: &mut Identifiers);

String: String = {
    r#""(\\"|[^"])*""# => String::from(&<>[1..<>.len() - 1])
}

Identifier: Identifier = {
    r"\p{ID_Start}\p{ID_Continue}*" => identifiers.intern(<>)
};

Expression: ast::Expression<()> = {
    <variable:Identifier> => ast::Expression {
        kind: ast::ExpressionKind::VariableAccess(variable),
        type_: ()
    },
    <target:Expression> "." <name:Identifier> => ast::Expression {
        kind: ast::ExpressionKind::FieldAccess(Box::new(target), name),
        type_: ()
    },
    <target:Expression> "(" ")" => ast::Expression {
        kind: ast::ExpressionKind::Call(Box::new(target)),
        type_: ()
    }
}

Statement: ast::Statement<()> = {
    <expression:Expression> ";" => ast::Statement::Expression(expression)
}

FunctionPrototype: ast::FunctionPrototype = {
    "fn" <name:Identifier> "(" ")" => ast::FunctionPrototype {
        name
    }
}

Function: ast::Function<UncheckedFunctionType> = {
    <prototype:FunctionPrototype> "{" <statements:(<Statement>)*> "}" => ast::Function { 
        type_: UncheckedFunctionType::new(UncheckedFunctionTypeKind::Statements(statements)),
        prototype
    },
    // TODO real support for attributes
    "#" "[" "extern" "(" <external_name:String> ")" "]"
    <prototype:FunctionPrototype> ";" => ast::Function {
        type_: UncheckedFunctionType::new(UncheckedFunctionTypeKind::External(external_name)),
        prototype
    }
}

Class: ast::Class<(), UncheckedFunctionType> = {
    "class" <name:Identifier> "{"
        <functions:(<Function>)*>
    "}" => ast::Class {
        name,
        functions,
        type_: ()
    }
}

Declaration: ast::Declaration<(), UncheckedFunctionType> = {
    <class:Class> => ast::Declaration::Class(class),
}

pub SourceFile: ast::SourceFile<(), UncheckedFunctionType> = { 
    <declarations:(<Declaration>)*> => ast::SourceFile {
        declarations
    }
}
